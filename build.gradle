plugins {
    id "groovy"
    id "com.github.johnrengelman.shadow"   version "7.1.2"
    id "io.micronaut.application"          version "3.6.3"
    id "com.adarshr.test-logger"           version "3.2.0"
    id "jacoco"
}

version = "0.1"
group = "com.tim4it.payment.comparison"

repositories {
    mavenCentral()
}

ext {
    annotationApi           = "1.3.2"
    lombok                  = "1.18.24"
    jackson                 = "2.14.0"
    swaggerVersion          = "2.2.6"
    micronautOpenApiVersion = "4.2.0"
}

dependencies {
    implementation          project(":payment-comparison-dto")

    annotationProcessor     "io.micronaut:micronaut-http-validation"
    implementation          "io.micronaut:micronaut-http-client"
    implementation          "io.micronaut:micronaut-jackson-databind"

    implementation          "jakarta.annotation:jakarta.annotation-api"
    runtimeOnly             "ch.qos.logback:logback-classic"
    compileOnly             "org.graalvm.nativeimage:svm"

    implementation          "io.micronaut:micronaut-validation"
    implementation          "io.micronaut.reactor:micronaut-reactor"

    // lombok
    compileOnly             "org.projectlombok:lombok:${lombok}"
    annotationProcessor     "org.projectlombok:lombok:${lombok}"

    // jackson
    implementation          "com.fasterxml.jackson.core:jackson-core:${jackson}"
    implementation          "com.fasterxml.jackson.core:jackson-annotations:${jackson}"
    implementation          "com.fasterxml.jackson.datatype:jackson-datatype-jsr310"

    implementation          "javax.annotation:javax.annotation-api:1.3.2"

    // swagger, open api
    annotationProcessor     "io.micronaut:micronaut-management:3.7.4"
    annotationProcessor     "io.micronaut.openapi:micronaut-openapi"
    implementation          "io.swagger.core.v3:swagger-annotations:${swaggerVersion}"
    implementation          "io.micronaut.openapi:micronaut-openapi:${micronautOpenApiVersion}"

    // html rendering engine
    implementation          "io.micronaut.views:micronaut-views-freemarker"

    // logging
    implementation          "ch.qos.logback:logback-classic"

    // testing
    testImplementation      "org.codehaus.groovy:groovy:3.0.13"
    testImplementation      "org.spockframework:spock-core:2.3-groovy-3.0"
    testImplementation      "io.micronaut.test:micronaut-test-spock"
}

application {
    mainClass.set("com.tim4it.payment.comparison.PaymentComparisonApplication")
}

java {
    sourceCompatibility = JavaVersion.toVersion("11")
    targetCompatibility = JavaVersion.toVersion("11")
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
}

jacoco {
    toolVersion = "0.8.8"
}

jacocoTestReport {
    reports {
        xml.required = false
        csv.required = false
        html.outputLocation = layout.buildDirectory.dir("${project.buildDir}/reports/jacoco/html")
    }
}

distZip.enabled = distTar.enabled = shadowDistTar.enabled = shadowDistZip.enabled = false

graalvmNative.toolchainDetection = false
micronaut {
    version("3.7.4")
    runtime("netty")
    testRuntime("spock2")
    processing {
        incremental(true)
        annotations("com.tim4it.*")
    }
}
